#!/bin/zsh
[ -z "$TMUX"  ] && exec tmux new-session

# Load aliases
source $XDG_CONFIG_HOME/shell/aliasrc

# Options
setopt autocd                   # Type directory name to cd into it.
setopt interactive_comments     # Allow comments in interactive shell.
setopt extendedglob             # Use ^ and # in glob patterns.
stty stop undef <$TTY >$TTY	    # Disable Ctrl-S to freeze terminal.

# History in cache directory
HISTSIZE=10000000
SAVEHIST=10000000
HISTFILE="${XDG_CACHE_HOME}/zsh/history"
[[ -f $HISTFILE ]] || mkdir -p $(dirname $HISTFILE)

# Basic auto/tab complete:
autoload -U compinit
zstyle ':completion:*' menu select
zmodload zsh/complist
compinit
_comp_options+=(globdots)		# Include hidden files.

# vi mode
bindkey -v
export KEYTIMEOUT=1

# Use vim keys in tab complete menu:
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -v '^?' backward-delete-char

# <Ctrl-e> opens file explorer TUI.
function explorer() {
  local tmp="$(mktemp -t 'yazi-cwd.XXXXX')" cwd
  yazi --cwd-file="$tmp"
  if cwd="$(command cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
		builtin cd -- "$cwd"
	fi
	'rm' -f -- "$tmp"
  # cd `yazi --cwd-file /tmp/yazicwd; cat /tmp/yazicwd`
  if (( $? == 0 )); then
    local precmd
    for precmd in $precmd_functions; do
      $precmd
    done
    zle reset-prompt
  fi
}
zle -N explorer
bindkey '^E' explorer

# <Ctrl-g> opens Git TUI.
function git-tui() {
  lazygit
  if (( $? == 0 )); then
    local precmd
    for precmd in $precmd_functions; do
      $precmd
    done
    zle reset-prompt
  fi
}
zle -N git-tui
bindkey '^G' git-tui



# Change cursor shape for different vi modes.
function zle-keymap-select () {
  case $KEYMAP in
    vicmd) echo -ne '\e[1 q';;      # block
    viins|main) echo -ne '\e[5 q';; # beam
  esac
}
zle -N zle-keymap-select
zle-line-init() { zle -K viins; echo -ne '\e[5 q' }
zle -N zle-line-init
echo -ne '\e[5 q' # Use beam shape cursor on startup.
preexec() { echo -ne '\e[5 q' ;} # Use beam shape cursor for each new prompt.

# starship.rs
eval "$(starship init zsh)"

# Plugins
# z
source $XDG_DATA_HOME/zsh/z/z.sh

# zsh-autosuggestions
source $XDG_DATA_HOME/zsh/zsh-autosuggestions/zsh-autosuggestions.zsh

# zsh-syntax-highlighting
source $XDG_DATA_HOME/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# zsh-history-substring-search
source $XDG_DATA_HOME/zsh/zsh-history-substring-search/zsh-history-substring-search.zsh
  # search history with up/down or j/k (in vi mode)
  bindkey '^[[A' history-substring-search-up
  bindkey '^[[B' history-substring-search-down
  bindkey '^j' history-substring-search-down
  bindkey '^k' history-substring-search-up
  bindkey -M vicmd 'k' history-substring-search-up
  bindkey -M vicmd 'j' history-substring-search-down
  export HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND=default
